plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'net.saliman.cobertura' version '2.2.7'
    id 'com.github.hierynomus.license' version '0.11.0'
}

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

group = 'com.github.lukaszbudnik.cloudtag'
archivesBaseName = 'cloudtag'
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.curator:curator-framework:2.7.1'
    compile 'org.apache.jclouds.provider:aws-ec2:1.9.0'
    compile 'org.apache.jclouds.provider:rackspace-cloudservers-us:1.9.0'
    compile 'org.apache.jclouds.provider:rackspace-cloudservers-uk:1.9.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

license {
    header project.file('HEADER')

    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Łukasz Budnik'
    ext.email = 'lukasz.budnik@gmail.com'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task printTestErrors {
    def xmlReports = new File("${buildDir}/test-results")
    if (xmlReports.exists()) {
        xmlReports.eachFileMatch(groovy.io.FileType.FILES, ~/TEST\-.*\.xml/) {
            def testsuite = new groovy.util.XmlParser().parse(it)
            testsuite.testcase.findAll {
                it.failure.size() > 0
            }.each {
                println it
            }
        }
    }
}

artifacts {
    archives javadocJar, sourcesJar, testJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

def deployUsername = hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('DEPLOY_USERNAME')

def deployPassword = hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('DEPLOY_PASSWORD')


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: deployUsername, password: deployPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: deployUsername, password: deployPassword)
            }

            pom.project {
                name 'cloudtag'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'CloudTag is a Curator ensemble provider which returns connection string based on cloud servers with matching tags.'
                url 'https://github.com/lukaszbudnik/jelvis'

                scm {
                    connection 'scm:git:git@github.com:lukaszbudnik/cloudtag.git'
                    developerConnection 'scm:git:git@github.com:lukaszbudnik/cloudtag.git'
                    url 'git@github.com:lukaszbudnik/cloudtag.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'lukaszbudnik'
                        name 'Łukasz Budnik'
                        email 'lukasz.budnik@gmail.com'
                    }
                }
            }
        }
    }
}
